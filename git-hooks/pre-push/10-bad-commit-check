#!/usr/bin/env perl

use strict;
use warnings;
use autodie;

sub slurp_command {
    my @command = @_;

    my ( $read, $write );

    pipe $read, $write;

    my $pid = fork();

    if($pid) {
        close $write;
        my @lines;
        while(<$read>) {
            chomp;
            push @lines, $_;
        }

        waitpid $pid, 0;

        return @lines;
    } else {
        close $read;
        open STDOUT, '>&', $write;
        exec @command;
    }
}

while(<STDIN>) {
    chomp;
    my ( $local_ref, $local_sha, $remote_ref, $remote_sha ) = split;

    my @lines = slurp_command('git', 'log', "--pretty=format:%h\t%aN\t%s",
            $remote_sha . '..' . $local_sha);

    foreach my $line (@lines) {
        my ( $commit_hash, $author_name, $commit ) = split /\t/, $line, 3;

        next unless $author_name =~ /\bhoelz\b/i;

        if($commit =~ /fixup!/) {
            print STDERR "Commit $commit_hash '$commit' is a fixup commit\n";
            exit 1;
        } elsif($commit =~ /don(?:')?t\s*push/i) { # XXX what about the body?
            print STDERR "Commit $commit_hash '$commit' says not to push it...\n";
            exit 1;
        } elsif($commit =~ /\bwip\b/i) {
            print STDERR "Commit $commit_hash '$commit' is a work-in-progress\n";
            exit 1;
        }
    }

    next if $local_ref eq 'delete';
}
